#!/usr/bin/env fish
# Component: Neovim
# Description: Install Neovim and lazy.nvim plugin manager
# Mode: All modes (if FEDPUNK_INSTALL_NEOVIM=true)

# Exit if not enabled in mode config
if not set -q FEDPUNK_INSTALL_NEOVIM; or test "$FEDPUNK_INSTALL_NEOVIM" != "true"
    exit 0
end

# Source helpers
if not set -q FEDPUNK_PATH
    set -gx FEDPUNK_PATH "{{ .chezmoi.sourceDir }}"
end
source "$FEDPUNK_PATH/home/core/lib/helpers.fish"

section "Neovim Installation"

# Install Neovim
if command -v nvim >/dev/null 2>&1
    success "Neovim already installed: "(nvim --version | head -1)
else
    step "Installing Neovim" "sudo dnf install -qy neovim"
end

# Install lazy.nvim plugin manager
subsection "Setting up lazy.nvim"
set lazy_path "$HOME/.local/share/nvim/lazy/lazy.nvim"
if not test -d "$lazy_path"
    step "Installing lazy.nvim" "git clone --quiet --filter=blob:none https://github.com/folke/lazy.nvim.git --branch=stable $lazy_path"
else
    success "lazy.nvim already installed"
end

# Install Neovim plugins
if test -f "$HOME/.config/nvim/init.lua"
    subsection "Installing Neovim plugins"
    step "Installing plugins" "nvim --headless '+Lazy! sync' +qa"
else
    warning "Neovim config not found (will be deployed by chezmoi)"
end

echo ""
box "Neovim Installation Complete!" $GUM_SUCCESS
